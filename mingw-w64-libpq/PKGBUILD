# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libpq
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=15.12
pkgrel=1
pkgdesc="Libraries for use with PostgreSQL (mingw-w64)"
arch=('any')
url="https://www.postgresql.org/"
license=('custom:PostgreSQL')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("https://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2"
        postgresql-12.0-mingw-link.patch
        postgresql-9.5.1-pl-perl.patch
        postgresql-9.5.1-pl-python.patch
        postgresql-9.4.1-pl-tcl.patch
        postgresql-9.4.1-mingw-enable-readline.patch
        postgresql-13.1-disable-wsa-invalid-event-static-assert.patch
        postgresql-13.1-pgevent-def.patch
        postgresql-14.0-use-mingw-setjmp-on-ucrt.patch
        postgresql-15.1-clang-16.patch
        only-static2.patch)
sha256sums=('3bc8462a38ca0857270cc88b949a3f6659f0d5c44c029c482355835b61a0f6f7'
            '607217b422349770d25af20f88e4a7925e68bb934232dff368c2ee59f24249a4'
            '99e6c8b9d58a9615c7bb1e7471b8e9c074add7bffc7d2cf742bafa38e9654e1f'
            'ced0ccf978eefc1ad0641803c9759b652e588eb552ec9179cd9dfc9d94ecf499'
            'b12c4e493845b3ead8950bbc26b1cc7bd4fb0dc3313fea4f7b281866d18c0402'
            'cd52d56833c60afdce98d91eece73c11978762671e36be7bd9c31a1ea0a0282d'
            '72c14a78eeafdd3c9a13c3e124b1941b5da090488c7bd73f08b3cd78bacd07d5'
            'ffaecbe5a38877728bddbf307b379b39c645dd1ffe53aa8d84dfa96807494764'
            'e65f664644faa5cbd392c575a13bace31799e19d16be64cee362ba0e4a361c23'
            '77876e4f8fd2500db2a1815628bd8d8bbd825563da19f8ea70ac9f9b6659414c'
            'SKIP')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${srcdir}/postgresql-${pkgver}
  apply_patch_with_msg \
    postgresql-9.5.1-pl-perl.patch \
    postgresql-9.5.1-pl-python.patch \
    postgresql-9.4.1-pl-tcl.patch \
    postgresql-13.1-disable-wsa-invalid-event-static-assert.patch \
    postgresql-13.1-pgevent-def.patch \
    postgresql-14.0-use-mingw-setjmp-on-ucrt.patch

    # Jeroens patch
    patch -p1 --fuzz=10 -i ${srcdir}/only-static2.patch

  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"
  LIBS="-lsecur32 -lws2_32 -lgdi32 -lcrypt32 -lwldap32" \
  ../postgresql-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --with-openssl \
    --with-ldap \
    --disable-thread-safety \
    --enable-integer-datetimes \
    --disable-nls \
    --disable-rpath \
    --without-libxml \
    --without-libxslt \
    --without-python \
    --without-perl \
    --without-tcl

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/"{bin,include,lib}

  make DESTDIR="${pkgdir}" -C "src/interfaces" install
  make DESTDIR="${pkgdir}" -C "src/common" install
  make DESTDIR="${pkgdir}" -C "src/port" install
  make DESTDIR="${pkgdir}" -C "src/include" install

  # Move dll's to bin directory
  # mv "${pkgdir}${MINGW_PREFIX}/lib/"*.dll "${pkgdir}${MINGW_PREFIX}/bin/"

  # these headers are needed by the not-so-public headers of the interfaces
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/include/{libpq,postgresql/internal/libpq}
  install -m644 ${srcdir}/postgresql-${pkgver}/src/include/c.h "${pkgdir}${MINGW_PREFIX}/include/postgresql/internal/"
  install -m644 ${srcdir}/postgresql-${pkgver}/src/include/port.h "${pkgdir}${MINGW_PREFIX}/include/postgresql/internal/"
  install -m644 ${srcdir}/postgresql-${pkgver}/src/include/postgres_fe.h "${pkgdir}${MINGW_PREFIX}/include/postgresql/internal/"
  install -m644 ${srcdir}/postgresql-${pkgver}/src/include/libpq/pqcomm.h "${pkgdir}${MINGW_PREFIX}/include/postgresql/internal/libpq/"

  # Fix pkg-config file
  sed -i 's/-lpq/-lpq -lpgport -lpgcommon/g' "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libpq.pc"
}
