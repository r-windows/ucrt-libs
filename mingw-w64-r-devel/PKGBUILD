# Maintainer: Gore Liu <goreliu@126.com>

_realname=r-devel
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.3.9000
pkgrel=1
pkgdesc="Software environment for statistical computing (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://www.r-project.org/'
license=('GPL-2.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-fc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "texinfo"
             "texinfo-tex")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-omp"
         "${MINGW_PACKAGE_PREFIX}-pcre2"
         "${MINGW_PACKAGE_PREFIX}-tk"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib")
checkdepends=("diffutils"
              "unzip")
source=("https://cran.r-project.org/src/base-prerelease/R-devel.tar.gz")
#source=("R-devel.tar.gz::https://cran.r-project.org/src/base-prerelease/R-devel_2023-12-27_r85742.tar.gz")
sha256sums=('SKIP')
noextract=(R-devel.tar.gz)

prepare() {
  rm -rf ${srcdir}/${_realname}-${pkgver}
  mkdir ${srcdir}/${_realname}-${pkgver}
  MSYS="winsymlinks:lnk" tar -xf "R-devel.tar.gz" -C ${srcdir}/${_realname}-${pkgver} --strip-components=1

  export PATH="$PATH:$(cygpath $LOCALAPPDATA)/Programs/MiKTeX/miktex/bin/x64:/c/progra~1/MiKTeX/miktex/bin/x64"
  echo "PATH: $PATH"
  texindex --version
  texi2any --version
  make --version
  perl --version
}

build() {
  rm -Rf build-${MSYSTEM}
  MSYS="winsymlinks:lnk" cp -Rf ${srcdir}/${_realname}-${pkgver} build-${MSYSTEM}
  cd build-${MSYSTEM}/src/gnuwin32
  echo "WIN =" > MkRules.local
  echo "ICU_LIBS = $(pkg-config --libs --static icu-i18n)" >> MkRules.local
  echo "CURL_LIBS = $(pkg-config --libs --static libcurl)" >> MkRules.local
  echo "CAIRO_LIBS = \"$(pkg-config --libs --static cairo)\"" >> MkRules.local
  echo "CAIRO_CPPFLAGS = \"$(pkg-config --cflags --static cairo)\"" >> MkRules.local
  if [[ ${MSYSTEM} == CLANG* ]]; then
    echo "USE_LLVM = 1" >> MkRules.local
  fi
  cat MkRules.local
  make all
  make cairodevices
  make recommended
}

check() {
  export MY_TCLTK="${MINGW_PREFIX}/lib"
  cd ${srcdir}/build-${MSYSTEM}/src/gnuwin32
  make check || true
}

package() {
  cd ${srcdir}/build-${MSYSTEM}/src/gnuwin32/installer
  sed -i '/manual/d' Makefile
  sed -i '/GW_INFO_FILES/d' Makefile
  sed -i '/dosfiles.zip/d' Makefile
  make imagedir
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  cp -Rv R-devel ${pkgdir}${MINGW_PREFIX}/R
}
