# Maintainer: Jeroen
_realname=curl
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=8.14.0
pkgrel=1
pkgdesc="Command line tool and library for transferring data with URLs. (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://curl.haxx.se/"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-autotools")
depends=("${MINGW_PACKAGE_PREFIX}-libssh2"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-nghttp2"
         "${MINGW_PACKAGE_PREFIX}-openssl")

options=('staticlibs')
source=("${url}/download/${_realname}-${pkgver}.tar.xz"
        "https://github.com/curl/curl/pull/17475.patch"
        "disable-cainfo-for-schannel.patch"
        "0001-Make-cURL-relocatable.patch"
        "0002-nghttp2-static.patch"
        "0004-more-static-fixes.patch")
sha256sums=('b3634cfc538c72c9c6ca794ef4c73d7fdbd171e8dee2db837d83a5e45231002a'
            'SKIP'
            'SKIP'
            'c4e6bfd5b58f944d75293128effbd22fe42ee0131b915d9230ceb3c004c0322d'
            'SKIP'
            'SKIP')
validpgpkeys=('914C533DF9B2ADA2204F586D78E11C6B279D5C91'  # Daniel Stenberg
              '27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'
              '4461EAF0F8E9097F48AF0555F9FEAFF9D34A1BDB')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${_realname}-${pkgver}"
  
  apply_patch_with_msg \
    0002-nghttp2-static.patch \
    disable-cainfo-for-schannel.patch \
    17475.patch

  autoreconf -vfi

}

build() {

 mkdir -p "${srcdir}/shared-${MSYSTEM}" && cd "${srcdir}/shared-${MSYSTEM}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-random \
    --disable-static \
    --enable-shared \
    --enable-sspi \
    --enable-ldap \
    --enable-ldaps \
    --without-libidn2 \
    --with-winidn \
    --with-libssh2 \
    --without-brotli \
    --without-zstd \
    --with-ldap-lib=wldap32 \
    --with-nghttp2 \
    --without-librtmp \
    --without-libpsl \
    --with-openssl \
    --with-schannel \
    --with-default-ssl-backend=schannel \
    --without-gnutls \
    --without-ca-bundle \
    --without-ca-path \
    --with-ca-fallback

  make

  # Fore pure static build:
  rm -fv /${MINGW_ARCH}/lib/*.dll.a
  export PKG_CONFIG="/${MINGW_ARCH}/bin/pkg-config --static"

  mkdir -p "${srcdir}/static-${MSYSTEM}" && cd "${srcdir}/static-${MSYSTEM}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-random \
    --enable-static \
    --disable-shared \
    --enable-sspi \
    --enable-ldap \
    --enable-ldaps \
    --without-libidn2 \
    --with-winidn \
    --with-libssh2 \
    --without-brotli \
    --without-zstd \
    --with-ldap-lib=wldap32 \
    --with-nghttp2 \
    --without-librtmp \
    --without-libpsl \
    --with-openssl \
    --with-schannel \
    --with-default-ssl-backend=schannel \
    --without-gnutls \
    --without-ca-bundle \
    --without-ca-path \
    --with-ca-fallback

  make
}

package() {
  cd "${srcdir}/shared-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  cd "${srcdir}/static-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  
  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
  sed -s "s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" -i ${pkgdir}${MINGW_PREFIX}/bin/curl-config
}
