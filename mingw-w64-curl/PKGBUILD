# Maintainer: Jeroen
_realname=curl
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=8.3.0
pkgrel=9000
pkgdesc="Command line tool and library for transferring data with URLs. (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://curl.haxx.se/"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-autotools")
depends=("${MINGW_PACKAGE_PREFIX}-libssh2"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-nghttp2"
         "${MINGW_PACKAGE_PREFIX}-openssl")

options=('staticlibs')
source=("${url}/download/${_realname}-${pkgver}.tar.xz"
        "disable-cainfo-for-schannel.patch"
        "0001-Make-cURL-relocatable.patch"
        "0002-nghttp2-static.patch"
        "0003-libpsl-static-libs.patch"
        "0004-more-static-fixes.patch")
sha256sums=('376d627767d6c4f05105ab6d497b0d9aba7111770dd9d995225478209c37ea63'
            'SKIP'
            'c4e6bfd5b58f944d75293128effbd22fe42ee0131b915d9230ceb3c004c0322d'
            '3ee9c75a3046f86f91290c143170179230c9adc6eabfbb79eb26f708a165b719'
            '7492d019036b5bec251bfbc3c0b40e5f16d3dd6b2515068835e087a6c21f19ad'
            '590eb65e90e756eaad993d52a101f29091ada2c742c5a607684e88fc5c560d54')
validpgpkeys=('914C533DF9B2ADA2204F586D78E11C6B279D5C91'  # Daniel Stenberg
              '27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'
              '4461EAF0F8E9097F48AF0555F9FEAFF9D34A1BDB')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/disable-cainfo-for-schannel.patch"

  apply_patch_with_msg \
    0002-nghttp2-static.patch \
    0003-libpsl-static-libs.patch \
    0004-more-static-fixes.patch

  autoreconf -vfi

}

build() {

 mkdir -p "${srcdir}/shared-${MSYSTEM}" && cd "${srcdir}/shared-${MSYSTEM}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-random \
    --disable-static \
    --enable-shared \
    --enable-sspi \
    --enable-ldap \
    --enable-ldaps \
    --without-libidn2 \
    --with-winidn \
    --with-libssh2 \
    --without-brotli \
    --without-zstd \
    --with-ldap-lib=wldap32 \
    --with-nghttp2 \
    --without-librtmp \
    --with-ssl \
    --with-schannel \
    --with-default-ssl-backend=schannel \
    --without-gnutls \
    --without-ca-bundle

  make

  # Fore pure static build:
  rm -fv /${MINGW_ARCH}/lib/*.dll.a
  export PKG_CONFIG="/${MINGW_ARCH}/bin/pkg-config --static"

  if [ "$MINGW_ARCH" == "clangarm64" ]; then
    _extra_config="--disable-pthreads"
  fi

  mkdir -p "${srcdir}/static-${MSYSTEM}" && cd "${srcdir}/static-${MSYSTEM}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-random \
    --enable-static \
    --disable-shared \
    --enable-sspi \
    --enable-ldap \
    --enable-ldaps \
    --without-libidn2 \
    --with-winidn \
    --with-libssh2 \
    --without-brotli \
    --without-zstd \
    --with-ldap-lib=wldap32 \
    --with-nghttp2 \
    --without-librtmp \
    --with-ssl \
    --with-schannel \
    --with-default-ssl-backend=schannel \
    --without-gnutls \
    --without-ca-bundle \
    ${_extra_config}

  make
}

package() {
  cd "${srcdir}/shared-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  cd "${srcdir}/static-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  
  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
  sed -s "s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" -i ${pkgdir}${MINGW_PREFIX}/bin/curl-config
}
